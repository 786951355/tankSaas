if (isModuleRun.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

def cfg = rootProject.ext.android
def appId = rootProject.ext.appId
android {
    compileSdkVersion cfg.compileSdkVersion
    buildToolsVersion cfg.buildToolsVersion
    defaultConfig {
        if (isModuleRun.toBoolean()) {
            applicationId appId["orders"]
        }
        minSdkVersion cfg.minSdkVersion
        targetSdkVersion cfg.targetSdkVersion
        versionCode cfg.versionCode
        versionName cfg.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            if (isModuleRun.toBoolean()) {
                manifestPlaceholders = [
                        JPUSH_PKGNAME: "com.zhaoguanche.tankerApp.inhouse",
                        JPUSH_APPKEY : "568fcfe5978e8e3b8b9b7407", //JPush上注册的包名对应的appkey(罐罐测试).
                        JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
                ]
            }
        }
    }

    sourceSets {
        main {
            if (isModuleRun.toBoolean()) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                manifest.properties.put("JPUSH_PKGNAME", "com.zhaoguanche.tankerApp.inhouse")
                manifest.properties.put("JPUSH_APPKEY", "568fcfe5978e8e3b8b9b7407")
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //release 时 debug 目录下文件不需要合并到主工程
                    exclude '**/debug/**'
                }
            }
        }
    }
    aaptOptions {
        cruncherEnabled = false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation project(path: ':resmodule')
    implementation project(path: ':basemodule')
}
